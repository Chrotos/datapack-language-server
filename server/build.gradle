import io.papermc.paperweight.userdev.ReobfArtifactConfiguration

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'java'
    id "io.papermc.paperweight.userdev" version "1.7.1"
    id "xyz.jpenilla.run-paper" version "2.3.0"
}

group 'de.katzen48.datapack'
version '1.0-SNAPSHOT'

repositories {
    gradlePluginPortal()
    mavenCentral()
    maven {
        url "https://papermc.io/repo/repository/maven-public/"
    }
}

ext {
    mcVersion = findProperty('mcVersion') ?: '1.21'
    mcVersionParts = mcVersion.split("\\.")
    mcMajor = mcVersionParts[0]
    mcMinor = mcVersionParts[1]
    mcPatch = mcVersionParts.size() > 2 ? mcVersion.split("\\.")[2] : '0'
}

dependencies {
    paperweight.paperDevBundle("${project.ext.mcVersion}-R0.1-SNAPSHOT")
    implementation "xyz.jpenilla:reflection-remapper:0.1.1"

    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.22.0'

    compileOnly 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
    annotationProcessor 'org.spigotmc:plugin-annotations:1.2.3-SNAPSHOT'
}

java {
    toolchain {
        if (project.ext.mcMinor.toInteger() < 20 || (project.ext.mcMinor.toInteger() == 20 && project.ext.mcPatch.toInteger() < 5)) {
            languageVersion = JavaLanguageVersion.of(17)
        } else {
            languageVersion = JavaLanguageVersion.of(21)
        }
    }
}

shadowJar {
    archiveFileName = 'plugin.jar'
}

tasks {
    runServer {
        minecraftVersion(project.ext.mcVersion)
    }

    compileJava {
        if (project.ext.mcMinor.toInteger() < 20 || (project.ext.mcMinor.toInteger() == 20 && project.ext.mcPatch.toInteger() < 5)) {
            options.release = 17
        } else {
            options.release = 21
        }
    }

    assemble {
        dependsOn(reobfJar)
    }

    sourceSets {
        main {
            java {
                if (project.ext.mcMinor.toInteger() < 20 || (project.ext.mcMinor.toInteger() == 20 && project.ext.mcPatch.toInteger() < 5)) {
                    exclude 'de/katzen48/datapack/converters/Version_1_20_5/**'
                }
            }
        }
    }
}